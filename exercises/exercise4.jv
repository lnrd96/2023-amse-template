

pipeline MobileGeraete {

    block MBExtractor oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    } // File

    pipe {
        from: MBExtractor;
        to: ZipArchiveInterpreter;
    }

    block ZipArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    } // FileSystem

    pipe {
        from: ZipArchiveInterpreter;
        to: MBFilePicker;
    }

    block MBFilePicker oftype FilePicker {
        path: "/data.csv";
    } // File

    pipe {
        from: MBFilePicker;
        to: MBTextFileInterpreter;
    } 
    
    block MBTextFileInterpreter oftype TextFileInterpreter {  

    } // TextFile
    
    pipe {
        from: MBTextFileInterpreter;
        to: MBCSVInterpreter;
    }

    block MBCSVInterpreter oftype CSVInterpreter {  
        delimiter: ";";
    } // Sheet
    
    pipe {
        from: MBCSVInterpreter;
        to: MBColumnDeleter;
    }

    block MBColumnDeleter oftype ColumnDeleter {
        delete: [column F, column G, column H, column I];
    } // Sheet
    
    pipe {
        from: MBColumnDeleter;
        to: MBTableInterpreter;
    }
    
    block MBTableInterpreter oftype TableInterpreter {
        header: false;
        columns: [
            "Geraet" oftype GTZ,
            "Hersteller" oftype text,
            "Model" oftype text,
            "Monat" oftype integer,
            "Temperatur" oftype decimal,
            "Batterietemperatur" oftype decimal,
            "Geraet aktiv" oftype text,
        ];
    } // Table

    pipe {
        from: MBTableInterpreter;
        to: TemperatureTransformer;
    }

  	block TemperatureTransformer oftype TableTransformer {
		inputColumn: "Temperatur";
		outputColumn: "Temperatur";
		use: CelsuisToFahrenheit;
	} // Table
    
    pipe {
        from: TemperatureTransformer;
        to: BatterieTemperatureTransformer;
    }
	
    block BatterieTemperatureTransformer oftype TableTransformer {
		inputColumn: "Batterietemperatur";
		outputColumn: "Batterietemperatur";
		use: CelsuisToFahrenheit;
	} // Table

	transform CelsuisToFahrenheit {
		from celsius oftype decimal;
		to fahrenheit oftype decimal;
		fahrenheit:  (celsius  * 9 / 5) + 32;
	} 

    pipe {
        from: BatterieTemperatureTransformer;
        to: MBLoader;
    }

    block MBLoader oftype SQLiteLoader {
        table: "temperatures";
        file: "temperatures.sqlite";
    } // Database

}

// GTZ

constraint GreaterThanZero oftype RangeConstraint {
  lowerBound: 0;
  lowerBoundInclusive: false;
}

valuetype GTZ oftype integer {  // workaround to remove empty cells
    constraints: [
        GreaterThanZero,
    ];
}
